global string $gMainWindow;
menu -p $gMainWindow -label "Cubic Stylization";
	menuItem -label "Offline Stylization" -command offlineStylization;
	menuItem -label "Real-time Stylization" -command "createNode transform -n CGGT1; createNode mesh -n CGGTShape1 -p CGGT1; sets -add initialShadingGroup CGGTShape1; createNode CGGTNode -n CGGTNode1; connectAttr pCubeShape1.outMesh CGGTNode1.input_geometry; connectAttr CGGTNode1.output_geometry CGGTShape1.inMesh;";
	
global proc offlineStylization() 
{
    if (`window -exists offlineWin`) 
        deleteUI offlineWin; 
    
    window -title "Offline Cubic Stylization" -widthHeight 500 500 offlineWin;
        columnLayout -columnAttach "both" 5 -rowSpacing 10 -columnWidth 500; 
            text -l "Tool Parameters" -align "left";
            
            frameLayout -collapsable true -label "Overall Cubeness";
            string $cubenessSlider = `floatSliderGrp -field true -minValue 0 -maxValue 10 -fieldMinValue 0 -fieldMaxValue 10 -precision 3 -label "Cubeness" cubenessSlider`;		
            textField objFilePath;
            button -label "Load Target Shape OBJ" -command loadOBJ; 
            
            frameLayout -collapsable true -label "Per-Axis Cubeness";
            string $cubenessXSlider = `floatSliderGrp -field true -minValue 0 -maxValue 10 -fieldMinValue 0 -fieldMaxValue 10 -precision 3 -label "Cubeness in X Axis" -enable 0 cubenessXSlider`; 
            string $cubenessYSlider = `floatSliderGrp -field true -minValue 0 -maxValue 10 -fieldMinValue 0 -fieldMaxValue 10 -precision 3 -label "Cubeness in Y Axis" -enable 0 cubenessYSlider`; 
            string $cubenessZSlider = `floatSliderGrp -field true -minValue 0 -maxValue 10 -fieldMinValue 0 -fieldMaxValue 10 -precision 3 -label "Cubeness in Z Axis" -enable 0 cubenessZSlider`;   
            checkBox -label "Use Per-Axis Cubeness" -changeCommand toggleAxisSliderEnable perAxisCheck; 
            
            frameLayout -collapsable true -label "Per-Vertex Cubeness";
			//image -width 100 -height 100 -backgroundColor 0.0 0.0 0.0 gaussMapImg;
            textField gaussFilePath;      
            button -label "Load Gauss Map" -command loadGauss -enable 0 gaussButton; 
            checkBox -label "Use Gauss Map" -changeCommand toggleVertexSliderEnable perVertexCheck;
            
            frameLayout -collapsable true -label "Extra Deformation Attributes"; 
            string $iterationsSlider = `floatSliderGrp -field true -minValue 1 -maxValue 10 -fieldMinValue 1 -fieldMaxValue 10 -value 1.0 -precision 1 -label "Iterations" iterationsSlider`; 
            string $reductionSlider = `floatSliderGrp -field true -minValue 0 -maxValue 99.9 -fieldMinValue 0 -fieldMaxValue 99.9 -precision 1 -label "Reduction Percentage" reductionSlider`; 
            string $FORDropdown = `optionMenuGrp -label "Frame of Reference" refframe`; menuItem -label "Global"; menuItem -label "Local"; 
                 
		rowColumnLayout -numberOfColumns 2 -columnWidth 1 250 -columnWidth 2 250;
			button -label "Deform" -w 70 -h 30 -command cubicStylization; 
			button -label "Reset Mesh" -w 70 -h 30 -command resetMesh;
    showWindow offlineWin;
}

global proc loadOBJ()
{
    string $f[] = `fileDialog2 -fileMode 1`; 
    string $filename = $f[0];
    textField -edit -text $filename objFilePath;
}

global proc loadGauss()
{
    string $f[] = `fileDialog2 -fileMode 1`; 
    string $filename = $f[0];
    textField -edit -text $filename gaussFilePath;
	//image -edit -image $filename gaussMapImg;
}

global proc toggleAxisSliderEnable()
{
    string $perAxisChecked = `checkBox -q -value perAxisCheck`; 
    if ($perAxisChecked == "1") { 
        floatSliderGrp -edit -enable 1 cubenessXSlider;
        floatSliderGrp -edit -enable 1 cubenessYSlider;
        floatSliderGrp -edit -enable 1 cubenessZSlider;
		floatSliderGrp -edit -enable 0 cubenessSlider;
		checkBox -edit -value 0 perVertexCheck;
		textField -edit -enable 0 gaussFilePath;
        button -edit -enable 0 gaussButton;
    }
    else {
        floatSliderGrp -edit -enable 0 cubenessXSlider;
        floatSliderGrp -edit -enable 0 cubenessYSlider;
        floatSliderGrp -edit -enable 0 cubenessZSlider;
		floatSliderGrp -edit -enable 1 cubenessSlider;
    }
}

global proc toggleVertexSliderEnable()
{
    string $perAxisChecked = `checkBox -q -value perVertexCheck`; 
    if ($perAxisChecked == "1") { 
		textField -edit -enable 1 gaussFilePath;
        button -edit -enable 1 gaussButton;
		floatSliderGrp -edit -enable 1 cubenessSlider;
		checkBox -edit -value 0 perAxisCheck;
		floatSliderGrp -edit -enable 0 cubenessXSlider;
        floatSliderGrp -edit -enable 0 cubenessYSlider;
        floatSliderGrp -edit -enable 0 cubenessZSlider;
    }
    else {
		textField -edit -enable 0 gaussFilePath;
        button -edit -enable 0 gaussButton;
		floatSliderGrp -edit -enable 1 cubenessSlider;
    }
}

global proc cubicStylization()
{
    float $cubeVal = `floatSliderGrp -q -value cubenessSlider`;
    int $itVal = `floatSliderGrp -q -value iterationsSlider`;
    float $redVal = `floatSliderGrp -q -value reductionSlider`;
    string $frameVal = `optionMenuGrp -q -value refframe`;
    float $cubeXVal = `floatSliderGrp -q -value cubenessXSlider`;
    float $cubeYVal = `floatSliderGrp -q -value cubenessYSlider`;
    float $cubeZVal = `floatSliderGrp -q -value cubenessZSlider`;
    string $useAxisVal = `checkBox -q -value perAxisCheck`;
    string $useGaussVal = `checkBox -q -value perVertexCheck`;
	string $gaussMap = `textField -q -text gaussFilePath`;
    string $targetOBJ = `textField -q -text objFilePath`;
	string $rstMesh = "no";
    
    CGGTCmd -c $cubeVal -it $itVal -r $redVal -f $frameVal -cx $cubeXVal -cy $cubeYVal -cz $cubeZVal -ua $useAxisVal -ug $useGaussVal -gm $gaussMap -to $targetOBJ -re $rstMesh;
}

global proc resetMesh()
{
    float $cubeVal = `floatSliderGrp -q -value cubenessSlider`;
    int $itVal = `floatSliderGrp -q -value iterationsSlider`;
    float $redVal = `floatSliderGrp -q -value reductionSlider`;
    string $frameVal = `optionMenuGrp -q -value refframe`;
    float $cubeXVal = `floatSliderGrp -q -value cubenessXSlider`;
    float $cubeYVal = `floatSliderGrp -q -value cubenessYSlider`;
    float $cubeZVal = `floatSliderGrp -q -value cubenessZSlider`;
    string $useAxisVal = `checkBox -q -value perAxisCheck`;
    string $useGaussVal = `checkBox -q -value perVertexCheck`;
	string $gaussMap = `textField -q -text gaussFilePath`;
    string $targetOBJ = `textField -q -text objFilePath`;
	string $rstMesh = "reset";
    
    CGGTCmd -c $cubeVal -it $itVal -r $redVal -f $frameVal -cx $cubeXVal -cy $cubeYVal -cz $cubeZVal -ua $useAxisVal -ug $useGaussVal -gm $gaussMap -to $targetOBJ -re $rstMesh;
}
